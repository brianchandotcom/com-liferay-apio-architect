configurations.all {
	resolutionStrategy {
		force 'xml-apis:xml-apis:1.4.01'
	}
}

allprojects {
	apply plugin: 'jacoco'

	clean {
		delete 'out'
	}

	check {
		dependsOn checkSourceFormatting
	}
}

configure(subprojects.findAll {!it.childProjects}) {
	buildCSS {
		enabled = false
	}

	configJSModules {
		enabled = false
	}

	copyLibs {
		enabled = false
	}

	processResources {
		enabled = false
	}

	replaceSoyTranslation {
		enabled = false
	}

	setUpTestableTomcat {
		enabled = false
	}

	startTestableTomcat {
		enabled = false
	}

	stopTestableTomcat {
		enabled = false
	}

	test {
		ignoreFailures = false
	}

	transpileJS {
		enabled = false
	}

	wrapSoyAlloyTemplate {
		enabled = false
	}
}

task codeCoverageReport(type: JacocoReport)

codeCoverageReport {
	executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

	subprojects.findAll { !it.childProjects }.each { sourceSets it.sourceSets.main }

	afterEvaluate {
		classDirectories = files(
			classDirectories.files.collect {
				fileTree(
					dir: it,
					exclude: [
						"**/com/liferay/apio/architect/internal/test/**"
					])
			})
	}

	reports {
		csv {
			enabled = false
		}

		html {
			enabled = false
		}

		xml {
			enabled = true
			destination "${buildDir}/reports/jacoco/report.xml"
		}
	}
}