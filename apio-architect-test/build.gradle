import aQute.bnd.gradle.Resolve
import aQute.bnd.gradle.TestOSGi

buildscript {
	dependencies {
		classpath group: "biz.aQute.bnd", name: "biz.aQute.bnd.gradle", version: "4.0.0"
	}

	repositories {
		mavenCentral()
	}
}

task copyJacocoAgent(type: Copy)
task resolve(type: Resolve)
task testOSGi(type: TestOSGi)

copyJacocoAgent {
	from {
		zipTree(configurations.jacocoAgent.singleFile)
	}

	into "build/jacoco"
}

dependencies {
	compileInclude group: "commons-collections", name: "commons-collections", version: "3.0"
	compileInclude group: "org.skyscreamer", name: "jsonassert", version: "1.1.1"
	compileInclude group: "uk.co.datumedge", name: "hamcrest-json", version: "0.2"

	compileOnly group: "io.vavr", name: "vavr", version: "0.9.2"
	compileOnly group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compileOnly group: "org.apache.aries.spec", name: "org.apache.aries.javax.jax.rs-api", version: "1.0.2-20181005.084226-2"
	compileOnly group: "org.apache.felix", name: "org.apache.felix.scr", version: "2.0.8"
	compileOnly group: "org.json", name: "json", version: "20180813"
	compileOnly group: "org.osgi", name: "org.osgi.core", version: "6.0.0"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	compileOnly group: "org.osgi", name: "org.osgi.service.jaxrs", version: "1.0.0"
	compileOnly group: "org.osgi", name: "org.osgi.util.function", version: "1.1.0"
	compileOnly group: "org.osgi", name: "org.osgi.util.promise", version: "1.1.0"
	compileOnly group: "org.osgi", name: "osgi.enroute.hamcrest.wrapper", version: "1.3.0"
	compileOnly group: "org.osgi", name: "osgi.enroute.junit.wrapper", version: "4.12.0"
	compileOnly project(":apps:apio-architect:apio-architect-api")
	compileOnly project(":apps:apio-architect:apio-architect-exception-mapper-impl")
	compileOnly project(":apps:apio-architect:apio-architect-impl")
	compileOnly project(":apps:apio-architect:apio-architect-sample")
	compileOnly project(":apps:apio-architect:apio-architect-test-fragment")
	compileOnly project(":apps:apio-architect:apio-architect-uri-mapper-impl")

	runtime group: "ch.qos.logback", name: "logback-classic", version: "1.2.3"
	runtime group: "org.apache.aries.jax.rs", name: "org.apache.aries.jax.rs.whiteboard", version: "1.0.2-20181005.084327-2"
	runtime group: "org.apache.felix", name: "org.apache.felix.configadmin", version: "1.8.14"
	runtime group: "org.apache.felix", name: "org.apache.felix.eventadmin", version: "1.4.8"
	runtime group: "org.apache.felix", name: "org.apache.felix.http.jetty", version: "3.4.0"
	runtime group: "org.eclipse.platform", name: "org.eclipse.osgi", version: "3.13.0"
}

deploy {
	enabled = false
}

resolve {
	description 'Resolves the "test.bndrun" file and checks if the "runBundles" section is up-to-date.'

	group 'verification'

	bndrun 'test.bndrun'

	failOnChanges true

	bundles configurations.compileOnly
}

testIntegration {
	dependsOn testOSGi
}

testOSGi {
	description 'Runs the Apio Architect integration tests.'

	group 'verification'

	bndrun 'test.bndrun'

	dependsOn copyJacocoAgent
	dependsOn resolve

	bundles configurations.compileOnly
}